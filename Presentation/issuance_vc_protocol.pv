type skey.
type pkey.
type signature.

(* Hàm về mã hóa*)
fun pk(skey): pkey.
fun sign(bitstring, skey): signature.
reduc forall m: bitstring, k: skey; verifysign(sign(m, k), pk(k)) = m.
fun aenc(bitstring, pkey ) : bitstring.
reduc forall m: bitstring , k : skey ; adec(aenc(m, pk(k)), k) = m.

free didU: bitstring.
free didB: bitstring.
free skU: skey [private].
free skB: skey [private].
free VC_User: bitstring [private]. 

(* Kênh truyền thông công khai mà attacker có thể nghe/chặn (Dolev-Yao) *)
free c_vcrequest: channel.
free c_resolve: channel.  (*resolve DID *)

(* Sự kiện đánh dấu các bước quan trọng để kiểm chứng *)
(*did, nonce*)
event userStart(bitstring,bitstring).
(*did, nonce*)
event VCIssued(bitstring, bitstring).

let DIDResolver =
  in(c_resolve, did: bitstring);
  if did = didU then
    out(c_resolve, pk(skU))
  else if did = didB then
    out(c_resolve, pk(skB))
  else
    0.

(* Tiến trình của User *)
let UserProcess = 
    out(c_resolve, didB);
    in(c_resolve, pkB: pkey);
    let pkU = pk(skU) in
    (* Gửi yêu cầu đăng nhập *)
    out(c_vcrequest, aenc(didU, pkB));
    (* Nhận nonce được mã hóa từ Bank *)
    in(c_vcrequest, ResB: bitstring);
    (* Kiểm tra phản hồi từ Bank *)
    let (=didU, nonceB: bitstring) = adec(ResB, skU) in    
    (* Đánh dấu sự kiện User bắt đầu quá trình cấp VC *)
    event userStart(didU, nonceB);
    let sign_data = (didU, nonceB) in
    (* Gửi chữ ký để xác thực *)
    out(c_vcrequest, aenc((didU, nonceB, sign(sign_data, skU)) , pkB));
    (* Nhận VC từ Bank *)
    in(c_vcrequest, token_enc: bitstring).

(* Tiến trình của Bank *)
let BankProcess =
    in(c_vcrequest, authenReq: bitstring);
    let (didReq:bitstring) = adec(authenReq, skB) in 
    out(c_resolve, didReq);
    in(c_resolve, pkReq: pkey);
    let (=didU) = (didReq) in
    let pkU = pk(skU) in
    (*let (=pkU) = pkReq in*)
    (* Bank nhận yêu cầu: didReq là DID từ requester *)
    new nonceB: bitstring;
    (* Gửi nonce đã mã hóa *)
    out(c_vcrequest, aenc((didReq, nonceB), pkReq));
    (* Nhận bằng chứng xác thực từ User *)
    in(c_vcrequest, proof_res_enc: bitstring);
    (* Kiểm tra điều kiện an ninh trên VP *)
    let proof_res = adec(proof_res_enc, skB) in
    let (=didReq, =nonceB, signReq: signature) = proof_res in
    let (=didReq, =nonceB) = verifysign(signReq, pkReq) in 
    (* Tạo VC và gửi cho User *)
    out(c_vcrequest, aenc(VC_User, pkReq));
    (* Đánh dấu sự kiện Bank phát hành VC cho DID tương ứng *)
    event VCIssued(didReq, nonceB).

query attacker(VC_User).

(*query didFReq:bitstring, nonce:bitstring;
  inj-event(VCIssued(didFReq, nonce)) ==>
  inj-event(userStart(didFReq, nonce)).

query nonce: bitstring; event(VCIssued(didU, nonce)).*)

process  !UserProcess | !BankProcess | !DIDResolver
