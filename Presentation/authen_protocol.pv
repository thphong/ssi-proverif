type skey.
type pkey.
type signature.

(* Hàm về mã hóa*)
fun pk(skey): pkey.
fun sign(bitstring, skey): signature.
reduc forall m: bitstring, k: skey; verifysign(sign(m, k), pk(k)) = m.
fun aenc(bitstring, pkey ) : bitstring.
reduc forall m: bitstring , k : skey ; adec(aenc(m, pk(k)), k) = m.

free didU: bitstring.
free didB: bitstring.
free skU: skey [private].
free skB: skey [private].
free dataU: bitstring [private].    (* Dữ liệu trong VC (ví dụ thông tin người dùng) *)
free tokenU: bitstring [private]. 

(* Kênh truyền thông công khai mà attacker có thể nghe/chặn (Dolev-Yao) *)
free c_auth: channel.
free c_resolve: channel.  (* [private] /resolve DID *)

(* Sự kiện đánh dấu các bước quan trọng để kiểm chứng *)
event userStart(bitstring,bitstring).    (* a1 bắt đầu xác thực, access vô tài khoản a2, với nonce *)
event tokenIssued(bitstring, bitstring).  (* Bank phát hành token cho a1, access vô tài khoản à, với nonce *)

let DIDResolver =
  in(c_resolve, did: bitstring);
  if did = didU then
    out(c_resolve, pk(skU))
  else if did = didB then
    out(c_resolve, pk(skB))
  else
    0.

(* Tiến trình của User: khởi xướng và thực hiện đăng nhập *)
let UserProcess = 
    out(c_resolve, didB);
    in(c_resolve, pkB: pkey);
    (* Kiểm tra public key của bank *)
    let pkU = pk(skU) in
    (* Gửi yêu cầu đăng nhập *)
    out(c_auth, aenc((didU, pkU), pkB));
    (* Nhận nonce được mã hóa từ Bank *)
    in(c_auth, ResB: bitstring);
    (* Kiểm tra phản hồi từ Bank trên nonceU *)
    let (=didU, nonceB: bitstring) = adec(ResB, skU) in    
    (* Đánh dấu sự kiện User bắt đầu quá trình xác thực cho didU *)
    event userStart(didU, nonceB);
    (* Bước 3: Tạo VP gồm (nonce, didU, VC, chữ ký của User) *)
    let sign_data = (didU, nonceB) in
    out(c_auth, aenc((didU, nonceB, sign(sign_data, skU)) , pkB));
    (* Bước 4: Nhận accessToken từ Bank *)
    in(c_auth, token_enc: bitstring).

(* Tiến trình của Bank: chờ yêu cầu và đáp ứng *)
let BankProcess =
    in(c_auth, authenReq: bitstring);
    let (didReq:bitstring, pkReq: pkey) = adec(authenReq, skB) in    
    let pkU = pk(skU) in
    (* Bank nhận yêu cầu: didReq là DID từ requester *)
    new nonceB: bitstring;
    (* Gửi nonce đã mã hóa *)
    out(c_auth, aenc((didReq, nonceB), pkReq));
    (* Nhận VP từ User *)
    in(c_auth, prest_Res_enc: bitstring);
    (* Kiểm tra điều kiện an ninh trên VP *)
    let prest_Res = adec(prest_Res_enc, skB) in
    let (=didReq, =nonceB, signReq: signature) = prest_Res in
    let (=didReq, =nonceB) = verifysign(signReq, pkReq) in 
    (* Tạo accessToken mới và gửi cho User *)
    (*new token: bitstring;*)
    let (=didU) = (didReq) in
    let (=pkU) = pkReq in
    out(c_auth, aenc(tokenU, pkReq));
    (* Đánh dấu sự kiện Bank phát hành token cho DID tương ứng *)
    event tokenIssued(didReq, nonceB).

query attacker(tokenU).

query didFReq:bitstring, n: bitstring;
  inj-event(tokenIssued(didFReq, n)) ==>
  inj-event(userStart(didFReq, n)).

query n: bitstring; event(tokenIssued(didU, n)).

(*query n: bitstring; event(tokenIssued(didU, n)).

query n: bitstring; event(tokenIssued(didU, didU, n)).

query attacker(skU).
query attacker(skB).
*)

process  !UserProcess | !BankProcess | !DIDResolver

 (*
 Đã thử nghiệm
 - Lộ VC: bị tấn công
 - Lộ VP: bị tấn công
 - Không gửi PkU khi rquest login:  Không bị tấn công
 - Bị thay đổi public key của User trong registry: TPP không thể đăng nhập và access tài khoản của User
 - Bị thay đổi public key của Bank trong registry:  Cả User và TPP không thể đăng nhập và access tài khoản của User
 - Lợi ích: dù cho attacker giả mạo trang web giả, cũng ko tấn công được, vì đã có bước kiểm tra pkB trong chữ kí của VC
 - Nhược điểm: chưa chứng minh được khóa vòng, revoke, zkp
 *)